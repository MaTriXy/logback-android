apply plugin: 'com.github.johnrengelman.shadow'

def projectName = rootProject.name

/**
 * Creates a shaded uber jar, containing the core and classic
 * modules along with brut and mindprod packages.
 */
shadowJar {
  dependencies {
    include(project(':logback-android'))
    // FIXME: How do we exclude META-INF/*? Using `exclude 'META-INF/*'
    // does not work, so the shadow jar ends up with the META-INF
    // from apktool-lib.
    include(dependency('com.github.tony19:apktool-lib'))
  }
  baseName projectName
  relocate 'brut', 'ch.qos.logback.repackage.brut'
  relocate 'com.mindprod', 'ch.qos.logback.repackage.com.mindprod'
}

jar {
  manifest {
    attributes(
      'Implementation-Title': projectName,
      'Implementation-Version': rootProject.version,
      'Implementation-Vendor-Id': rootProject.group,
      'Implementation-Vendor': rootProject.group,
      'Build-Jdk': JavaVersion.current()
    )
  }
}

/**
 * Creates a shaded uber jar, and copies it into the root
 * build directory
 */
task uberjar(type: Copy) {
  description 'Assembles a jar archive containing the classes of the "core" and "classic" modules.'
  from shadowJar
  into rootProject.buildDir
  rename { String fileName ->
    fileName - '-all.jar' + '.jar'
  }
}
